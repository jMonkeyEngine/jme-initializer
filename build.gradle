plugins {
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'idea'
	id "com.github.node-gradle.node" version "3.1.1"
	id "org.owasp.dependencycheck" version "7.0.4.1" //for security scanning of dependencies
}

group = 'com.jmonkeyengine'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencyCheck {
	failBuildOnCVSS=5 //significant vulnerabilities will prevent a build being completed
}

node {
	download = true
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
	implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
	implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.0.3'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.0.3'
}

test {
	useJUnitPlatform()
}

task buildReactApp(type: NodeTask, dependsOn: 'npmInstall') {
	script = project.file('node_modules/webpack/bin/webpack.js')
	args = [
			'--mode', 'development',
			'--entry', './src/main/resources/react/reactgameForm.jsx',
			'-o', './src/main/resources/static/js/dist/react-app.js'
	]
}

processResources.dependsOn 'buildReactApp'
clean.delete << file('node_modules')
clean.delete << file('src/main/resources/static/js/dist')

bootJar.dependsOn 'dependencyCheckAnalyze' // <-- This step is quite slow, for experimental builds you may wish to temporarily comment it out